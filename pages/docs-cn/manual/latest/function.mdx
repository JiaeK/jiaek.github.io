---
title: "函数"
description: "Function syntax in ReScript"
canonical: "/docs/manual/latest/function"
---

<!--
# Function
-->
# 函数

<!--
_Cheat sheet for the full function syntax at the end_.
-->

_完整的函数语法清单在最后。_

<!--
ReScript functions are declared with an arrow and return an expression, just like JS functions. They compile to clean JS functions too.
-->

ReScript 函数用箭头声明，并返回一个表达式，就像 JS 函数一样。它们也可以编译成干净的 JS 函数。

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
let greet = (name) => "Hello " ++ name
```
```js
function greet(name) {
  return "Hello " + name;
}
```

</CodeTab>

<!--
This declares a function and assigns to it the name `greet`, which you can call like so:
-->

这声明了一个函数并给它指定了 `greet` 这个名字，你可以这样调用它：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
greet("world!") // "Hello world!"
```
```js
greet("world!");
```

</CodeTab>

<!--
Multi-arguments functions have arguments separated by comma:
-->

多参数函数的参数用逗号分隔：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let add = (x, y, z) => x + y + z
add(1, 2, 3) // 6
```
```js
function add(x, y, z) {
  return (x + y | 0) + z | 0;
}
```

</CodeTab>

<!--
For longer functions, you'd surround the body with a block:
-->

对于较长的函数，你得用 `{}` 将函数体围起来：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let greetMore = (name) => {
  let part1 = "Hello"
  part1 ++ " " ++ name
}
```
```js
function greetMore(name) {
  return "Hello " + name;
}
```

</CodeTab>

<!--
If your function has no argument, just write `let greetMore = () => {...}`.
-->

如果你的函数没有参数，只需要写 `let greetMore = () => {...}`。

<!--
## Labeled Arguments
-->
## 标签参数

<!--
Multi-arguments functions, especially those whose arguments are of the same type, can be confusing to call.
-->

多参数函数，特别是那些参数类型相同的函数，在调用时可能会很混乱。

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let addCoordinates = (x, y) => {
  // use x and y here
}
// ...
addCoordinates(5, 6) // which is x, which is y?
```
```js
function addCoordinates(x, y) {
  // use x and y here
}

addCoordinates(5, 6);
```

</CodeTab>

<!--
You can attach labels to an argument by prefixing the name with the `~` symbol:
-->

你可以为参数附加标签，在参数名前面加上 `~` 符号：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let addCoordinates = (~x, ~y) => {
  // use x and y here
}
// ...
addCoordinates(~x=5, ~y=6)
```
```js
function addCoordinates(x, y) {
  // use x and y here
}

addCoordinates(5, 6);
```

</CodeTab>

<!--
You can provide the arguments in **any order**:
-->

你可以按**任意顺序**传入这些参数：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
addCoordinates(~y=6, ~x=5)
```
```js
addCoordinates(5, 6);
```

</CodeTab>

<!--
The `~x` part in the declaration means the function accepts an argument labeled `x` and can refer to it in the function body by the same name. You can also refer to the arguments inside the function body by a different name for conciseness:
-->

声明中的 `~x` 部分意味着该函数接受一个标签为 `x` 的参数，并且可以在函数体中用相同的名称来指代它。你也可以在函数体中用不同的名字指代参数，这样更简洁：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius as r, ~color as c) => {
  setColor(c)
  startAt(r, r)
  // ...
}

drawCircle(~radius=10, ~color="red")
```
```js
function drawCircle(r, c) {
  setColor(c);
  return startAt(r, r);
}

drawCircle(10, "red");
```

</CodeTab>

<!--
As a matter of fact, `(~radius)` is just a shorthand for `(~radius as radius)`.
-->

事实上，`(~radius)` 只是 `(~radius as radius)` 的一个缩写。

<!--
Here's the syntax for typing the arguments:
-->

下面是给参数标记类型的语法：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius as r: int, ~color as c: string) => {
  // code here
}
```
```js
function drawCircle(r, c) {
  // code here
}
```

</CodeTab>

<!--
## Optional Labeled Arguments
-->
## 可选的标签参数

<!--
Labeled function arguments can be made optional during declaration. You can then omit them when calling the function.
-->

带标签的函数参数可以在声明时设置成可选的。你可以在调用函数时省略它们。

<CodeTab labels={["ReScript", "JS Output"]}>

```res
// radius can be omitted
let drawCircle = (~color, ~radius=?, ()) => {
  setColor(color)
  switch radius {
  | None => startAt(1, 1)
  | Some(r_) => startAt(r_, r_)
  }
}
```
```js
var Caml_option = require("./stdlib/caml_option.js");

function drawCircle(color, radius, param) {
  setColor(color);
  if (radius === undefined) {
    return startAt(1, 1);
  }
  var r_ = Caml_option.valFromOption(radius);
  return startAt(r_, r_);
}
```

</CodeTab>

<!--
When given in this syntax, `radius` is **wrapped** in the standard library's `option` type, defaulting to `None`. If provided, it'll be wrapped with a `Some`. So `radius`'s type value is `None | Some(int)` here.
-->

当使用这种语法的时候，`radius` 被**包装**在标准库的 `option` 类型中，默认值为 `None`。如果函数调用提供了这个参数，那么它会被包装到 `Some` 中。所以这里 `radius` 的类型是 `None| Some(int)`。

<!--
More on `option` type [here](null-undefined-option.md).
-->

[这里](null-undefined-option.md)有更多关于 `option` 类型的信息。

<!--
**Note** for the sake of the type system, whenever you have an optional argument, you need to ensure that there's also at least one positional argument (aka non-labeled, non-optional argument) after it. If there's none, provide a dummy `unit` (aka `()`) argument.
-->

**注意**：因为类型系统的限制，只要函数有一个可选参数，你就需要确保在它后面至少有一个位置参数（又称非标记的，非可选的参数）。如果没有，可以提供一个虚拟的 `unit`（又表示为 `()`）参数。

<!--
### Signatures and Type Annotations
-->
### 类型签名和类型标注

<!--
Functions with optional labeled arguments can be confusing when it comes to signature and type annotations. Indeed, the type of an optional labeled argument looks different depending on whether you're calling the function, or working inside the function body. Outside the function, a raw value is either passed in (`int`, for example), or left off entirely. Inside the function, the parameter is always there, but its value is an option (`option<int>`). This means that the type signature is different, depending on whether you're writing out the function type, or the parameter type annotation. The first being a raw value, and the second being an option.
-->

当涉及到类型签名和类型标注时，带有可选标记参数的函数可能会令人困惑。事实上，一个可选标记参数的类型在不同地方看起来是不同的，这取决于你是正在调用函数，还是在函数体内部。在函数外部，一个原生值要么被传入（比如 `int`），要么不传入。在函数内部，参数总是存在的，但其值是一个 `option` 值（`option<int>`）。这意味着类型签名是不同的，这取决于你标注的是函数类型还是参数类型，前者是一个原生值，而后者是一个 `option`。

<!--
If we get back to our previous example and both add a signature and type annotations to its argument, we get this:
-->

如果我们回到之前的例子，同时给它的参数添加一个签名和类型标注，我们会得到这样的结果：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle: (~color: color, ~radius: int=?, unit) => unit =
  (~color: color, ~radius: option<int>=?, ()) => {
    setColor(color)
    switch radius {
    | None => startAt(1, 1)
    | Some(r_) => startAt(r_, r_)
    }
  }
```
```js
function drawCircle(color, radius, param) {
  setColor(color);
  if (radius !== undefined) {
    return startAt(radius, radius);
  } else {
    return startAt(1, 1);
  }
}
```

</CodeTab>

<!--
The first line is the function's signature, we would define it like that in an interface file (see [Signatures](module.md#signatures)). The function's signature describes the types that the **outside world** interacts with, hence the type `int` for `radius` because it indeed expects an `int` when called.
-->

第一行是函数的签名，我们会在一个接口文件中这样定义它（见[签名](module.md#signatures)章节）。函数的签名描述了**外部世界**与函数交互的类型，因此 `radius` 的类型是 `int`，因为它在调用时确实期望得到一个 `int`。

<!--
In the second line, we annotate the arguments to help us remember the types of the arguments when we use them **inside** the function's body, here indeed `radius` will be an `option<int>` inside the function.
-->

在第二行中，我们对参数进行标注，以帮助我们在**函数内部**使用参数时记住参数的类型，这里 `radius` 在函数内部将是一个 `option<int>`。

<!--
So if you happen to struggle when writing the signature of a function with optional labeled arguments, try to remember this!
-->

如果你在编写带有可选标记参数的函数的类型签名时遇到了困难，请记住参数类型内外有别！

<!--
### Explicitly Passed Optional
-->
### 显式传入的 `option` 值

<!--
Sometimes, you might want to forward a value to a function without knowing whether the value is `None` or `Some(a)`. Naively, you'd do:
-->

有时，你想将值转发给函数，但不确定值是 `None` 还是 `Some(a)`。你可能会这样做：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let result =
  switch payloadRadius {
  | None => drawCircle(~color, ())
  | Some(r) => drawCircle(~color, ~radius=r, ())
  }
```
```js
var r = payloadRadius;

var result = r !== undefined
  ? drawCircle(color, Caml_option.valFromOption(r), undefined)
  : drawCircle(color, undefined);
```

</CodeTab>

<!--
This quickly gets tedious. We provide a shortcut:
-->

这会变得很啰嗦。我们提供了一个简写：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let result = drawCircle(~color, ~radius=?payloadRadius, ())
```
```js
var result = drawCircle(1, undefined, undefined);
```

</CodeTab>

<!--
This means "I understand `radius` is optional, and that when I pass it a value it needs to be an `int`, but I don't know whether the value I'm passing is `None` or `Some(val)`, so I'll pass you the whole `option` wrapper".
-->

这样做的意思是“我知道 `radius` 是可选值，当我给它传值时，它需要是一个 `int`，但我不确定传的值是 `None` 还是 `Some(val)`，所以我传整个 `option` 包装给你”。

<!--
### Optional with Default Value
-->
### 有默认值的可选参数

<!--
Optional labeled arguments can also be provided a default value. In this case, they aren't wrapped in an `option` type.
-->

可选的标签参数也可以提供一个默认值，这种情况下，它们不会被包装在 `option` 类型中。

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius=1, ~color, ()) => {
  setColor(color)
  startAt(radius, radius)
}
```
```js
function drawCircle(radiusOpt, color, param) {
  var radius = radiusOpt !== undefined ? radiusOpt : 1;
  setColor(color);
  return startAt(radius, radius);
}
```

</CodeTab>

<!--
## Recursive Functions
-->
## 递归函数

<!--
ReScript chooses the sane default of preventing a function to be called recursively within itself. To make a function recursive, add the `rec` keyword after the `let`:
-->

ReScript 默认不允许函数在它内部调用自身。要创建一个递归函数，需要在 `let` 后面加上 `rec` 关键字：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let rec neverTerminate = () => neverTerminate()
```
```js
function neverTerminate(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}
```

</CodeTab>

<!--
A simple recursive function may look like this:
-->

一个简单的递归函数可能看起来像这样：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
// Recursively check every item on the list until one equals the `item`
// argument. If a match is found, return `true`, otherwise return `false`
let rec listHas = (list, item) =>
  switch list {
  | list{} => false
  | list{a, ...rest} => a === item || listHas(rest, item)
  }
```
```js
function listHas(_list, item) {
  while(true) {
    var list = _list;
    if (!list) {
      return false;
    }
    if (list.hd === item) {
      return true;
    }
    _list = list.tl;
    continue ;
  };
}
```

</CodeTab>

<!--
Recursively calling a function is bad for performance and the call stack. However, ReScript intelligently compiles [tail recursion](https://stackoverflow.com/questions/33923/what-is-tail-recursion) into a fast JavaScript loop. Try checking the JS output of the above code!
-->

递归调用一个函数对性能和调用栈不利。不过 ReScript 会智能地将[尾递归](https://stackoverflow.com/questions/33923/what-is-tail-recursion)编译为快速的 JS 循环。试着检查一下上面代码的 JS 输出！

<!--
### Mutually Recursive Functions
-->
### 互递归函数

<!--
Mutually recursive functions start like a single recursive function using the
`rec` keyword, and then are chained together with `and`:
-->

互递归函数像递归函数一样使用 `rec` 关键字开头，然后用 `and` 关键字串在一起：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let rec callSecond = () => callFirst()
and callFirst = () => callSecond()
```
```js
function callSecond(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}

function callFirst(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}
```

</CodeTab>

<!--
## Uncurried Function
-->
## 去柯里化的函数

<!--
ReScript's functions are curried by default, which is one of the few performance penalties we pay in the compiled JS output. The compiler does a best-effort job at removing those currying whenever possible. However, in certain edge cases, you might want guaranteed uncurrying. In those cases, put a dot in the function's parameter list:
-->

ReScript 的函数是默认柯里化的，这是少数的编译到 JS 之后付出的性能代价之一。编译器会尽最大可能去除这些柯里化。然而，在一些边缘情况下，你可能希望保证函数没有柯里化。这时可以在函数的参数列表前放一个 `.`：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let add = (. x, y) => x + y

add(. 1, 2)
```
```js
function add(x, y) {
  return x + y | 0;
}

add(1, 2);
```

</CodeTab>

<!--
If you write down the uncurried function's type, you'll add a dot there as well.
-->

如果你为去柯里化函数标注了类型，你也要在那加一个 `.`。

<!--
**Note**: both the declaration site and the call site need to have the uncurry annotation. That's part of the guarantee/requirement.
-->

**注意**：声明和调用位置都要加上去柯里化的标记。

<!--
**This feature seems trivial**, but is actually one of our most important features, as a primarily functional language. We encourage you to use it if you'd like to remove any mention of `Curry` runtime in the JS output.
-->

**这个特性看起来似乎微不足道**，但实际上是 ReScript 作为一门函数式语言最重要的特性之一。如果你想要在 JS 输出中移除所有运行时`柯里化`，我们鼓励你使用它。

<!--
## Async/Await(from v10.1)
-->
## Async/Await（自 v10.1 起）

<!--
Just as in JS, an async function can be declared by adding `async` before the definition, and `await` can be used in the body of such functions.
The output looks like idiomatic JS:
-->

就像在 JS 中一样，可以通过在定义前添加 `async` 来声明一个异步函数，可以在这类函数中使用 `await`。输出结果看上去和 JS 没多少区别：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let getUserName = async (userId) => userId

let greetUser = async (userId) => {
  let name = await getUserName(userId)
  "Hello " ++ name ++ "!"
}
```
```js
async function greetUser(userId) {
  var name = await getUserName(userId);
  return "Hello " + name + "!";
}
```
</CodeTab>

<!--
The return type of `getUser` is inferred to be `promise<string>`.
Similarly, `await getUserName(userId)` returns a `string` when the function returns `promise<string>`.
Using `await` outside of an `async` function (including in a non-async callback to an async function) is an error.
-->

`getUser` 的返回类型被推断为 `promise<string>`。类似地，`await getUserName(userID)` 在函数返回 `promise<string>` 时返回一个 `string`。在 `async` 函数之外使用 `await`（包括在异步函数的非异步回调中）是一个错误。

<!--
### Ergonomic error handling
-->
### 人性化的错误处理

<!--
Error handling is done by simply using `try`/`catch`, or a switch with an `exception` case, just as in functions that are not async.
Both JS exceptions and exceptions defined in ReScript can be caught. The compiler takes care of packaging JS exceptions into the builtin `JsError` exception:
-->

错误处理是通过简单地使用 `try/catch` 或者匹配 `exception` 的 switch 来完成的，就像在非异步的函数中一样。JS 异常和 ReScript 中定义的异常都可被捕获。编译器负责将 JS 异常打包成内置的 `JsError` 异常：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
exception SomeReScriptException

let somethingThatMightThrow = async () => raise(SomeReScriptException)

let someAsyncFn = async () => {
  switch await somethingThatMightThrow() {
  | data => Some(data)
  | exception JsError(_) => None
  | exception SomeReScriptException => None
  }
}
```
```js
var SomeReScriptException = /* @__PURE__ */Caml_exceptions.create("Example.SomeReScriptException");

async function someAsyncFn(param) {
  var data;
  try {
    data = await somethingThatMightThrow(undefined);
  }
  catch (raw_exn){
    var exn = Caml_js_exceptions.internalToOCamlException(raw_exn);
    if (exn.RE_EXN_ID === "JsError") {
      return ;
    }
    if (exn.RE_EXN_ID === SomeReScriptException) {
      return ;
    }
    throw exn;
  }
  return data;
}
```
</CodeTab>

<!--
## The ignore() Function
-->
## ignore() 函数

<!--
Occasionally you may want to ignore the return value of a function. ReScript provides an `ignore()` function that discards the value of its argument and returns `()`:
-->

偶尔你可能想忽略一个函数的返回值，ReScript 提供了一个 `ignore()` 函数，它抛弃传入的值并返回 `()`：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
mySideEffect()->Promise.catch(handleError)->ignore

Js.Global.setTimeout(myFunc, 1000)->ignore
```

```js
$$Promise.$$catch(mySideEffect(), function (prim) {
  return handleError(prim);
});

setTimeout(function (prim) {
  myFunc();
}, 1000);
```

</CodeTab>

<!--
## Tips & Tricks
-->
## 技巧和诀窍

<!--
Cheat sheet for the function syntaxes:
-->
有关函数的语法清单：

<!--
### Declaration
-->
### 函数声明

```res
// anonymous function
(x, y) => 1
// bind to a name
let add = (x, y) => 1

// labeled
let add = (~first as x, ~second as y) => x + y
// with punning sugar
let add = (~first, ~second) => first + second

// labeled with default value
let add = (~first as x=1, ~second as y=2) => x + y
// with punning
let add = (~first=1, ~second=2) => first + second

// optional
let add = (~first as x=?, ~second as y=?) => switch x {...}
// with punning
let add = (~first=?, ~second=?) => switch first {...}
```

<!--
#### With Type Annotation
-->
#### 带类型标注的函数声明

```res
// anonymous function
(x: int, y: int): int => 1
// bind to a name
let add = (x: int, y: int): int => 1

// labeled
let add = (~first as x: int, ~second as y: int) : int => x + y
// with punning sugar
let add = (~first: int, ~second: int) : int => first + second

// labeled with default value
let add = (~first as x: int=1, ~second as y: int=2) : int => x + y
// with punning sugar
let add = (~first: int=1, ~second: int=2) : int => first + second

// optional
let add = (~first as x: option<int>=?, ~second as y: option<int>=?) : int => switch x {...}
// with punning sugar
// note that the caller would pass an `int`, not `option<int>`
// Inside the function, `first` and `second` are `option<int>`.
let add = (~first: option<int>=?, ~second: option<int>=?) : int => switch first {...}
```

<!--
### Application
-->
### 应用函数

```res
add(x, y)

// labeled
add(~first=1, ~second=2)
// with punning sugar
add(~first, ~second)

// application with default value. Same as normal application
add(~first=1, ~second=2)

// explicit optional application
add(~first=?Some(1), ~second=?Some(2))
// with punning
add(~first?, ~second?)
```

<!--
#### With Type Annotation
-->
#### 在应用时加上类型标注

```res
// labeled
add(~first=1: int, ~second=2: int)
// with punning sugar
add(~first: int, ~second: int)

// application with default value. Same as normal application
add(~first=1: int, ~second=2: int)

// explicit optional application
add(~first=?Some(1): option<int>, ~second=?Some(2): option<int>)
// no punning sugar when you want to type annotate
```

<!--
### Standalone Type Signature
-->
### 独立的类型签名

```res
// first arg type, second arg type, return type
type add = (int, int) => int

// labeled
type add = (~first: int, ~second: int) => int

// labeled
type add = (~first: int=?, ~second: int=?, unit) => int
```

<!--
#### In Interface Files
-->
#### 在接口文件中加上类型签名

<!--
To annotate a function from the implementation file (`.res`) in your interface file (`.resi`):
-->

在你的接口文件（`.resi`）中为一个来自实现文件（`.res`）的函数加上类型注解：

```res sig
let add: (int, int) => int
```

<!--
The type annotation part is the same as the previous section on With Type Annotation.
-->

类型标注部分和上面的是一样的。

<!--
**Don't** confuse `let add: myType` with `type add = myType`. When used in `.resi` interface files, the former exports the binding `add` while annotating it as type `myType`. The latter exports the type `add`, whose value is the type `myType`.
-->

**不要混淆** `let add: myType` 和 `type add = myType`。在 `.resi` 接口文件中使用时，前者导出绑定 `add`，并将其类型标注为 `myType`；后者导出类型 `add`，其值为 `myType` 类型。
